/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        enabled: false
    ]

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        OPTIONAL INPUT CREATION
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: FAIDX {
        ext.args   = ''
    }

    withName: CREATESEQUENCEDICTIONARY {
        ext.args   = ''
    }

    withName: COMPOSESTRTABLEFILE {
        ext.args   = ''
    }

    withName: UNTAR {
        ext.args   = ''
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        GERMLINE VARIANT CALLING
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: BEDTOOLS_SPLIT {
        ext.args   = '--algorithm simple'
    }

    withName: CALIBRATEDRAGSTRMODEL {
        ext.args   = ''
    }

    withName: HAPLOTYPECALLER {
        publishDir  = [
            enabled: params.scatter_count <= 1 ? true : false,
            mode: params.publish_dir_mode,
            path: { "${params.outdir}/individuals/${meta.samplename}" },
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE if scatter count <= 1
        cpus        = { check_max( 1 * task.attempt, 'cpus' ) }
        ext.prefix  = {"${meta.id}.g"}
        ext.args    = '-ERC GVCF -contamination "0" -GQB 10 -GQB 20 -GQB 30 -GQB 40 -GQB 50 -GQB 60 -GQB 70 -GQB 80 -GQB 90 -G StandardAnnotation -G StandardHCAnnotation -G AS_StandardAnnotation --dragen-mode'
    }

    withName: BCFTOOLS_CONCAT {
        publishDir  = [
            enabled: params.scatter_count > 1 ? true : false,
            mode: params.publish_dir_mode,
            path: { "${params.outdir}/individuals/${meta.samplename}" },
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE if scatter count > 1
        ext.prefix = { "${meta.id}.g" }
        ext.args   = '-a'
    }

    withName: TABIX_GVCFS {
        publishDir = [
            path: { "${params.outdir}/individuals/${meta.samplename}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE
        ext.args   = ''
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        PREPROCESSING
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: REBLOCKGVCF {
        ext.args   = '-do-qual-approx --floor-blocks -GQB 20 -GQB 30 -GQB 40'
    }

    withName: COMBINEGVCFS {
        ext.args   = ''
    }

    withName: BCFTOOLS_MERGE {
        ext.args   = {"--gvcf $fasta -m none --output-type z --force-samples"}
    }

    withName: TABIX_COMBINED_GVCFS {
        ext.args   = ''
    }

    withName: GENOTYPE_GVCFS {
        ext.prefix = { "${meta.id}_genotyped" }
        ext.args   = '--allow-old-rms-mapping-quality-annotation-data -G StandardAnnotation -G AS_StandardAnnotation'
    }

    withName: BCFTOOLS_VIEW {
        ext.prefix = { "${meta.id}_viewed.g" }
        ext.args   = '-e \'QUAL="."\''
    }

    withName: BCFTOOLS_CONVERT {
        ext.prefix = { "${meta.id}_converted" }
        ext.args   = '--gvcf2vcf --output-type v'
    }

    withName: PEDFILTER {
        ext.prefix = { "${meta.id}_pedigree" }
    }

    withName: BGZIP_TABIX_PED_VCFS {
        publishDir = [
            enabled: params.output_mode == "seqr" ? true : false,
            path: { "${params.outdir}/families/${meta.family}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: FILTER_SNPS {
        ext.prefix = { "${meta.id}_filtered_snps" }
        if( params.output_mode == "seqplorer" ){
            ext.args = '-O v --soft-filter \'GATKCutoffSNP\' -e \'TYPE="snp" && (MQRankSum < -12.5 || ReadPosRankSum < -8.0 || QD < 2.0 || FS > 60.0 || (QD < 10.0 && AD[0:1] / (AD[0:1] + AD[0:0]) < 0.25 && ReadPosRankSum < 0.0) || MQ < 30.0)\' -m \'+\''
        }
        else if ( params.output_mode == "seqr" ){
            // TODO add seqr support (to be discussed) => don't forget to remove if statement in postprocess.nf!
            ext.args = ''
        }
    }

    withName: FILTER_INDELS {
        publishDir = [
            path: { "${params.outdir}/families/${meta.family}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE
        ext.prefix = { "${meta.id}_filtered_snps_indels" }
        if( params.output_mode == "seqplorer" ){
            ext.args = '-O v --soft-filter \'GATKCutoffIndel\' -e \'TYPE="indel" && (ReadPosRankSum < -20.0 || QD < 2.0 || FS > 200.0 || SOR > 10.0 || (QD < 10.0 && AD[0:1] / (AD[0:1] + AD[0:0]) < 0.25 && ReadPosRankSum < 0.0))\' -m \'+\''
        }
        else if ( params.output_mode == "seqr" ){
            // TODO add seqr support (to be discussed) => don't forget to remove if statement in postprocess.nf!
            ext.args = ''
        }
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        QUALITY CONTROL
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: BCFTOOLS_STATS {
        publishDir = [
            path: { "${params.outdir}/families/${meta.family}/reports" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE
    }

    withName: 'VCFTOOLS_.*'{
        publishDir = [
            path: { "${params.outdir}/families/${meta.family}/reports" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE
    }

    withName: VCFTOOLS_TSTV_COUNT{
        ext.args = "--TsTv-by-count"
    }

    withName: VCFTOOLS_TSTV_QUAL{
        ext.args = "--TsTv-by-qual"
    }

    withName: VCFTOOLS_SUMMARY{
        ext.args = "--FILTER-summary"
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        ANNOTATION
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: ENSEMBLVEP {
        // errorStrategy = { task.exitStatus = 255 ? 'retry' : 'terminate' }
        // maxRetries = 3
        // container = {params.vep_merged_cache ? "quay.io/biocontainers/ensembl-vep:${params.vep_version}--pl5321h4a94de4_${task.attempt - 1}" : "nfcore/vep:${params.vep_version}.${params.genome}"}
        container = "nfcore/vep:${params.vep_version}.${params.genome}"
        ext.args = [
            // don't contact external db
            '--offline',
            // increase buffer_size to speed up analysis                
            '--buffer_size 100000',
            // output format options      
            '--vcf --compress_output bgzip --force_overwrite', 
            // annotation options
            '--variant_class --sift b --polyphen b --humdiv --allele_number --numbers --total_length --gene_phenotype --ccds --regulatory',
            // identifiers
            '--hgvs --hgvsg --spdi --shift_hgvs 1 --protein --symbol --ccds --uniprot --tsl --appris --canoncical --mane --biotype --domains',
            // co-located variant info
            '--check_existing --clin_sig_allele --af --max_af --af_1kg --af_gnomad --pubmed --var_synonyms', 
            // plugins
            (params.vep_dbnsfp && params.dbnsfp)                                  ? "--plugin dbNSFP,${params.dbnsfp.split('/')[-1]},Ensembl_geneid,Ensembl_transcriptid,LRT_score,LRT_pred,MutationTaster_score,MutationTaster_pred,MutationAssessor_score,MutationAssessor_pred,PROVEAN_score,PROVEAN_pred,MetaSVM_score,MetaSVM_pred,MetaLR_score,MetaLR_pred,MetaRNN_score,MetaRNN_pred,M-CAP_score,M-CAP_pred,REVEL_score,BayesDel_addAF_score,BayesDel_addAF_pred,BayesDel_noAF_score,BayesDel_noAF_pred,CADD_phred,DANN_score,fathmm-MKL_coding_score,fathmm-MKL_coding_pred,GenoCanyon_score,gnomAD_exomes_AC,gnomAD_exomes_AN,gnomAD_exomes_AF,gnomAD_exomes_nhomalt,gnomAD_exomes_POPMAX_AF,gnomAD_genomes_AC,gnomAD_genomes_AN,gnomAD_genomes_AF,gnomAD_genomes_nhomalt,gnomAD_genomes_POPMAX_AF,Interpro_domain" : '',
            (params.vep_spliceai && params.spliceai_snv && params.spliceai_indel) ? "--plugin SpliceAI,snv=${params.spliceai_snv.split('/')[-1]},indel=${params.spliceai_indel.split('/')[-1]}" : '',
            (params.vep_spliceregion)                                             ? '--plugin SpliceRegion' : '',
            (params.vep_maxentscan && params.maxentscan)                          ? "--plugin MaxEntScan,${params.maxentscan.split('/')[-1]}" : '',
            (params.vep_eog && params.eog)                                        ? "--custom ${params.eog.split('/')[-1]},EOG,vcf,overlap,0,AF" : '',
            (params.vep_merged_cache)                                             ? '--merged' : '',
        ].join(' ').trim()
    }

    withName: BGZIP_ANNOTATED_VCFS {
        publishDir = [
            path: { "${params.outdir}/families/${meta.family}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : "${meta.family}.ann.vcf.gz" }
        ] // SAVE
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        FINAL PROCESSES
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    withName: VCF2DB{
        publishDir = [
            path: { "${params.outdir}/families/${meta.family}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE
    }

    withName: CUSTOM_DUMPSOFTWAREVERSIONS {
        cache = false
    }

    withName: MULTIQC {
        publishDir = [
            path: { "${params.outdir}/multiqc_reports" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ] // SAVE => Fix the location problem
        errorStrategy = {task.exitStatus == 143 ? 'retry' : 'ignore'}
        ext.args      = { params.multiqc_config ? "--config $multiqc_custom_config" : "" }
    }

}
